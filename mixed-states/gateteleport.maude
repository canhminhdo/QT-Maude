load quantum-circuits.maude

mod GATE-TELEPORT is
    pr CIRCUIT .

    var S : Scalar .

    --- defining initial states
    ops a b c d : -> Scalar .
    eq Pow(Abs(a), 2) .+ Pow(Abs(b), 2) = 1 .
    eq S .* Pow(Abs(a), 2) .+ S .* Pow(Abs(b), 2) = S .

    eq Pow(Abs(c), 2) .+ Pow(Abs(d), 2) = 1 .
    eq S .* Pow(Abs(c), 2) .+ S .* Pow(Abs(d), 2) = S .

    ops PSI PHI : -> Vector .
    ops pPSI pPHI : -> Matrix [memo] .
    eq PSI = a . |0> + b . |1> .
    eq PHI = c . |0> + d . |1> .
    eq pPSI = PSI x (PSI)^+ .
    eq pPHI = PHI x (PHI)^+ .

    op targetQState : -> Matrix [memo] .
    eq targetQState = unitary(
        pPHI (x) pPSI, CX(0, 1), 2
    ) .

    op ES : -> Ensemble .
    eq ES = {PSI (x) |0>  (x) |0> (x) |0> (x) |0> (x) PHI, 1} .

    op init : -> Config .
    eq init = {
        (#qbits: findN(ES))
        (mState: convert(ES))
        (actions: (
            H(1) CX(1, 2) H(3) CX(3, 4) CX(3, 2) 
            CX(1, 0) H(1) CX(5, 4) H(5)
            M(0) M(1) M(4) M(5)
            (c[4] == 1 ? X(2) X(3))
            (c[5] == 1 ? Z(3))
            (c[0] == 1 ? X(2))
            (c[1] == 1 ? Z(2) Z(3))
        ))
        (prob: 1)
        (bits: empty)
        (isEnd: false)
    } .
endm

load model-checker .

mod GATE-TELEPORT-PREDS is
    pr GATE-TELEPORT .
    inc SATISFACTION .

    subsort Config < State .

    var OCs : Soup{OComp} .
    var PROP : Prop .
    var MS : MixedState .
    var Prob : Scalar .
    var N : Nat .

    op isSuccess : -> Prop .
    eq {(isEnd: true) (mState: MS) (prob: Prob) (#qbits: N) OCs} |= isSuccess
    = Prob > 0 implies 
    tr[5](tr[4](tr[1](tr[0](MS, N), N), N), N)
    ==  I (x) I (x) targetQState (x) I (x) I .
    eq {OCs} |= PROP = false [owise] .
endm

mod GATE-TELEPORT-CHECK is
    inc GATE-TELEPORT-PREDS .
    inc MODEL-CHECKER .
    inc LTL-SIMPLIFIER .

    op gateProp : -> Formula .
    eq gateProp = <> isSuccess .
endm

in ../debug.maude

red modelCheck(init, gateProp) .